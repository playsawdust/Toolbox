buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.minecrell.licenser:net.minecrell.licenser.gradle.plugin:0.4.1"
	}
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

// grumble grumble...
if (!project.getPlugins().hasPlugin('net.minecrell.licenser')) {
	apply plugin: 'net.minecrell.licenser'
}

group = 'com.playsawdust'
archivesBaseName = 'toolbox'
version = '2.2.1'

repositories {
	mavenCentral()
}

dependencies {
	api 'org.slf4j:slf4j-api:1.7.9'
	api 'com.google.guava:listenablefuture:1.0'
	api 'org.checkerframework:checker-qual:3.21.0'
	
	compileOnly 'net.java.dev.jna:jna:5.0.0'
	compileOnly 'net.java.dev.jna:jna-platform:5.0.0'
}

license {
	header = file('HEADER')
	include '**/*.java'
	exclude '**/RandomXoshiro256StarStar.java'
}

java {
	withJavadocJar()
	withSourcesJar()
}

publishing {
	repositories {
		maven {
			// *shrug*
			url = "file:///home/una/unascribed.com/_subdomains/repo"
		}
	}
	publications {
		mavenJava(MavenPublication) {
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
			groupId = project.group
			artifactId = 'toolbox'
			version = project.version
			
			from components.java
			
			pom {
				name = 'Chipper Toolbox'
				description = 'A somewhat opinionated collection of assorted utilities for Java, designed for use with Google Guava.'
				url = 'https://github.com/playsawdust/Toolbox'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/playsawdust/Toolbox/blob/trunk/LICENSE'
					}
				}
				developers {
					developer {
						id = 'unascribed'
						name = 'Una Thompson'
						email = 'me@unascribed.com'
					}
					developer {
						id = 'Falkreon'
						name = 'Isaac Ellingson'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/playsawdust/Toolbox.git'
					developerConnection = 'scm:git:ssh://git@github.com:playsawdust/Toolbox.git'
					url = 'https://github.com/playsawdust/Toolbox'
				}
			}
		}
	}
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}
